/*
 * Copyright (c) 2025 WSO2 LLC. (http://www.wso2.org).
 *
 * WSO2 LLC. licenses this file to you under the Apache License,
 * Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

import org.apache.tools.ant.taskdefs.condition.Os
import org.gradle.api.attributes.java.TargetJvmEnvironment

plugins {
    id 'io.ballerina.plugin'
}

description = 'Ballerinax - Google Cloud Pub/Sub Ballerina'

def packageName = "gcloud.pubsub"
def packageOrg = "ballerinax"
def tomlVersion = stripBallerinaExtensionVersion("${project.version}")
def ballerinaTomlFilePlaceHolder = new File("${project.rootDir}/build-config/resources/Ballerina.toml")
def ballerinaTomlFile = new File("$project.projectDir/Ballerina.toml") 

def stripBallerinaExtensionVersion(String extVersion) {
    if (extVersion.matches(project.ext.timestampedVersionRegex)) {
        def splitVersion = extVersion.split('-')
        if (splitVersion.length > 3) {
            def strippedValues = splitVersion[0..-4]
            return strippedValues.join('-')
        } else {
            return extVersion
        }
    } else {
        return extVersion.replace("${project.ext.snapshotVersion}", "")
    }
}

ballerina {
    packageOrganization = packageOrg
    module = packageName
    langVersion = ballerinaLangVersion
    testCoverageParam = "--code-coverage --coverage-format=xml"
    isConnector = true
    platform = "java21"
}

configurations {
    externalJars {
        attributes {
            attribute(TargetJvmEnvironment.TARGET_JVM_ENVIRONMENT_ATTRIBUTE, objects.named(TargetJvmEnvironment, TargetJvmEnvironment.STANDARD_JVM))
        }
    }
}

dependencies {
    externalJars(group: 'com.google.cloud', name: 'google-cloud-pubsub', version: "${googleCloudPubSubVersion}") {
        transitive = false
    }
    externalJars(group: 'com.google.api', name: 'gax', version: "${googleGaxVersion}") {
        transitive = false
    }
    externalJars(group: 'com.google.api', name: 'gax-grpc', version: "${googleGaxVersion}") {
        transitive = false
    }
    externalJars(group: 'com.google.auth', name: 'google-auth-library-credentials', version: "${googleAuthLibraryVersion}") {
        transitive = false
    }
    externalJars(group: 'com.google.auth', name: 'google-auth-library-oauth2-http', version: "${googleAuthLibraryVersion}") {
        transitive = false
    }
    externalJars(group: 'io.grpc', name: 'grpc-netty-shaded', version: "${grpcVersion}") {
        transitive = false
    }
    externalJars(group: 'io.grpc', name: 'grpc-api', version: "${grpcVersion}") {
        transitive = false
    }
    externalJars(group: 'io.grpc', name: 'grpc-core', version: "${grpcVersion}") {
        transitive = false
    }
    externalJars(group: 'io.grpc', name: 'grpc-auth', version: "${grpcVersion}") {
        transitive = false
    }
    externalJars(group: 'io.grpc', name: 'grpc-stub', version: "${grpcVersion}") {
        transitive = false
    }
    externalJars(group: 'io.grpc', name: 'grpc-protobuf', version: "${grpcVersion}") {
        transitive = false
    }
    externalJars(group: 'io.grpc', name: 'grpc-protobuf-lite', version: "${grpcVersion}") {
        transitive = false
    }
    externalJars(group: 'io.grpc', name: 'grpc-context', version: "${grpcVersion}") {
        transitive = false
    }
    externalJars(group: 'com.google.api.grpc', name: 'proto-google-cloud-pubsub-v1', version: "${googleCloudPubsubApiVersion}") {
        transitive = false
    }
    externalJars(group: 'com.google.api.grpc', name: 'grpc-google-cloud-pubsub-v1', version: "${googleCloudPubsubApiVersion}") {
        transitive = false
    }
    externalJars(group: 'com.google.api.grpc', name: 'proto-google-common-protos', version: "${protoGoogleCommonProtosVersion}") {
        transitive = false
    }
    externalJars(group: 'com.google.api.grpc', name: 'proto-google-iam-v1', version: "${protoGoogleIamV1Version}") {
        transitive = false
    }
    externalJars(group: 'com.google.protobuf', name: 'protobuf-java', version: "${protobufVersion}") {
        transitive = false
    }
    externalJars(group: 'com.google.api', name: 'api-common', version: "${googleApiCommonVersion}") {
        transitive = false
    }
    externalJars(group: 'com.google.http-client', name: 'google-http-client', version: "${googleHttpClientVersion}") {
        transitive = false
    }
    externalJars(group: 'io.opencensus', name: 'opencensus-api', version: "${opencensusVersion}") {
        transitive = false
    }
    externalJars(group: 'io.opencensus', name: 'opencensus-contrib-http-util', version: "${opencensusVersion}") {
        transitive = false
    }
    externalJars(group: 'com.google.guava', name: 'guava', version: "${guavaVersion}") {
        transitive = false
    }
    externalJars(group: 'com.google.guava', name: 'failureaccess', version: "${guavaFailureaccessVersion}") {
        transitive = false
    }
    externalJars(group: 'org.threeten', name: 'threetenbp', version: "${threetenbpVersion}") {
        transitive = false
    }
    externalJars(group: 'com.google.j2objc', name: 'j2objc-annotations', version: "${j2objcAnnotationsVersion}") {
        transitive = false
    }
    externalJars(group: 'com.google.code.findbugs', name: 'jsr305', version: "${jsr305Version}") {
        transitive = false
    }
    externalJars(group: 'com.google.errorprone', name: 'error_prone_annotations', version: "${errorProneAnnotationsVersion}") {
        transitive = false
    }
    externalJars(group: 'com.google.api-client', name: 'google-api-client', version: "${googleApiClientVersion}") {
        transitive = false
    }
    externalJars(group: 'com.google.oauth-client', name: 'google-oauth-client', version: "${googleOauthClientVersion}") {
        transitive = false
    }
    externalJars(group: 'com.google.http-client', name: 'google-http-client-gson', version: "${googleHttpClientVersion}") {
        transitive = false
    }
    externalJars(group: 'io.perfmark', name: 'perfmark-api', version: "${perfmarkApiVersion}") {
        transitive = false
    }
}

task updateTomlFiles {
    doLast {
        def newConfig = ballerinaTomlFilePlaceHolder.text.replace("@project.version@", project.version)
        newConfig = newConfig.replace("@toml.version@", tomlVersion)
        newConfig = newConfig.replace("@gcloud.pubsub.version@", googleCloudPubSubVersion)
        newConfig = newConfig.replace("@gax.version@", googleGaxVersion)
        newConfig = newConfig.replace("@google.auth.version@", googleAuthLibraryVersion)
        newConfig = newConfig.replace("@grpc.version@", grpcVersion)
        newConfig = newConfig.replace("@google.grpc.version@", googleCloudPubsubApiVersion)
        newConfig = newConfig.replace("@google.proto.commons.version@", protoGoogleCommonProtosVersion)
        newConfig = newConfig.replace("@google.proto.iam.version@", protoGoogleIamV1Version)
        newConfig = newConfig.replace("@protobuf.version@", protobufVersion)
        newConfig = newConfig.replace("@google.api.common.version@", googleApiCommonVersion)
        newConfig = newConfig.replace("@google.http.client.version@", googleHttpClientVersion)
        newConfig = newConfig.replace("@opencensus.version@", opencensusVersion)
        newConfig = newConfig.replace("@guava.version@", guavaVersion)
        newConfig = newConfig.replace("@guava.failureaccess.version@", guavaFailureaccessVersion)
        newConfig = newConfig.replace("@threetenbp.version@", threetenbpVersion)
        newConfig = newConfig.replace("@j2objc.annotations.version@", j2objcAnnotationsVersion)
        newConfig = newConfig.replace("@jsr305.version@", jsr305Version)
        newConfig = newConfig.replace("@err.prone.annotation.version@", errorProneAnnotationsVersion)
        newConfig = newConfig.replace("@google.api.client.version@", googleApiClientVersion)
        newConfig = newConfig.replace("@google.oauth.client.version@", googleOauthClientVersion)
        newConfig = newConfig.replace("@google.http.client.version@", googleHttpClientVersion)
        newConfig = newConfig.replace("@perfmark.api.version@", perfmarkApiVersion)
        ballerinaTomlFile.text = newConfig
    }
}

task commitTomlFiles {
    doLast {
        project.exec {
            ignoreExitValue true
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "git commit -m '[Automated] Update the native jar versions' Ballerina.toml Dependencies.toml"
            } else {
                commandLine 'sh', '-c', "git commit -m '[Automated] Update the native jar versions' Ballerina.toml Dependencies.toml"
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact source: createArtifactZip, extension: 'zip'
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ballerina-platform/module-${packageOrg}-${packageName}")
            credentials {
                username = System.getenv("publishUser")
                password = System.getenv("publishPAT")
            }
        }
    }
}

clean {
    delete 'build'
}

// updateTomlFiles.dependsOn copyStdlibs

// build.dependsOn ":gcloud.pubsub-native:build"
// build.dependsOn "updateTomlFiles"
// build.dependsOn "initializeVariables"
// build.dependsOn ":gcloud.pubsub-native:jar"

// test.dependsOn ":gcloud.pubsub-native:build"

build.dependsOn copyToLib
build.dependsOn ":${packageName}-native:build"
build.dependsOn generatePomFileForMavenPublication

test.dependsOn ":${packageName}-native:build"

publish.dependsOn build
publishToMavenLocal.dependsOn build
