/*
 * Copyright (c) 2025 WSO2 LLC. (http://www.wso2.org).
 *
 * WSO2 LLC. licenses this file to you under the Apache License,
 * Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

plugins {
    id 'java'
    id 'checkstyle'
    id 'com.github.spotbugs'
}

description = 'Ballerinax - Google Cloud Pub/Sub Java Utils'

configurations.all {
    resolutionStrategy {
        // Force use of secure versions to fix CVE-2024-7254 and CVE-2025-55163
        force "com.google.protobuf:protobuf-java:${protobufVersion}"
        force "com.google.protobuf:protobuf-java-util:${protobufVersion}"
        force "io.grpc:grpc-netty-shaded:${grpcVersion}"
        force "io.grpc:grpc-api:${grpcVersion}"
        force "io.grpc:grpc-core:${grpcVersion}"
        force "io.grpc:grpc-stub:${grpcVersion}"
        force "io.grpc:grpc-protobuf:${grpcVersion}"
        force "io.grpc:grpc-protobuf-lite:${grpcVersion}"
        force "io.grpc:grpc-auth:${grpcVersion}"
        force "io.grpc:grpc-context:${grpcVersion}"
        force "io.grpc:grpc-alts:${grpcVersion}"
        force "io.grpc:grpc-googleapis:${grpcVersion}"
        force "io.grpc:grpc-grpclb:${grpcVersion}"
        force "io.grpc:grpc-inprocess:${grpcVersion}"
        force "io.grpc:grpc-services:${grpcVersion}"
        force "io.grpc:grpc-util:${grpcVersion}"
        force "io.grpc:grpc-xds:${grpcVersion}"
    }
}

dependencies {
    implementation group: 'org.ballerinalang', name: 'ballerina-lang', version: "${ballerinaLangVersion}"
    implementation group: 'org.ballerinalang', name: 'ballerina-runtime', version: "${ballerinaLangVersion}"
    implementation group: 'org.ballerinalang', name: 'value', version: "${ballerinaLangVersion}"
    implementation group: 'io.ballerina.stdlib', name: 'time-native', version: "${stdlibTimeVersion}"
    implementation group: 'com.google.cloud', name: 'google-cloud-pubsub', version: "${googleCloudPubSubVersion}"
    implementation group: 'io.grpc', name: 'grpc-netty-shaded', version: "${grpcVersion}"
    implementation group: 'com.google.protobuf', name: 'protobuf-java', version: "${protobufVersion}"
    implementation group: 'com.google.guava', name: 'guava', version: "${guavaVersion}"
    implementation group: 'com.google.api', name: 'gax', version: "${gaxVersion}"
    implementation group: 'com.google.auth', name: 'google-auth-library-oauth2-http', version: "${googleAuthVersion}"
    implementation group: 'org.threeten', name: 'threetenbp', version: "${threeTenVersion}"
    implementation group: 'org.slf4j', name: 'slf4j-jdk14', version: "${slf4jVersion}"
    compileOnly group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: "${spotbugsVersion}"
}

checkstyle {
    toolVersion "${checkStylePluginVersion}"
    configFile rootProject.file("build-config/checkstyle/build/checkstyle.xml")
}

def excludePattern = '**/module-info.java'
tasks.withType(Checkstyle) {
    exclude excludePattern
}

checkstyleMain.dependsOn(":checkstyle:downloadCheckstyleRuleFiles")

spotbugsMain {
    def classLoader = plugins["com.github.spotbugs"].class.classLoader
    def SpotBugsConfidence = classLoader.findLoadedClass("com.github.spotbugs.snom.Confidence")
    def SpotBugsEffort = classLoader.findLoadedClass("com.github.spotbugs.snom.Effort")
    effort = SpotBugsEffort.MAX
    reportLevel = SpotBugsConfidence.LOW
    reportsDir = file("$project.buildDir/reports/spotbugs")
    reports {
        html.enabled true
        text.enabled = true
    }
    def excludeFile = file("${rootDir}/spotbugs-exclude.xml")
    if(excludeFile.exists()) {
        excludeFilter = excludeFile
    }
}

spotbugsTest {
    enabled = false
}
